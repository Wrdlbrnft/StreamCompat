apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    compile 'com.android.support:support-v4:26.1.0'
    testCompile 'junit:junit:4.12'
}

final bintrayUser = System.getenv('BINTRAY_USER') ?: ''
final bintrayApiKey = System.getenv('BINTRAY_API_KEY') ?: ''
final versionName = "0.3.0.${System.getenv('TRAVIS_BUILD_NUMBER') ?: '0'}"

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'source'
}

task javadoc(type: Javadoc, dependsOn: 'assembleRelease') {
    source = android.sourceSets.main.java.srcDirs
    title = 'StreamCompat'
    options.links('http://docs.oracle.com/javase/8/docs/api/');
    options.linksOffline('http://d.android.com/reference', "${android.sdkDirectory}/docs/reference");
    classpath += files(android.libraryVariants.javaCompile.classpath.files)
    classpath += files(android.getBootClasspath())
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        Bintray(MavenPublication) {
            groupId 'com.github.wrdlbrnft'
            artifactId 'stream-compat'
            version versionName
            artifact sourceJar
            artifact javadocJar
            artifact new File(buildDir, '/outputs/aar/StreamCompat-release.aar')
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['Bintray']
    pkg {
        repo = 'maven'
        name = 'StreamCompat'
        userOrg = bintrayUser
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Wrdlbrnft/StreamCompat'
        publicDownloadNumbers = true
        version {
            name = versionName
            released = new Date()
        }
    }
}