apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 'android-N'
    buildToolsVersion "24.0.0 rc1"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 'N'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {

}

String bintrayUser = hasProperty('bintray_user') ? property('bintray_user') : ''
String bintrayApiKey = hasProperty('bintray_api_key') ? property('bintray_api_key') : ''
String versionName = hasProperty('version_name') ? property('version_name') : ''

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        Bintray(MavenPublication) {
            groupId 'com.github.wrdlbrnft'
            artifactId 'stream-compat'
            version versionName
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/StreamCompat-release.aar")
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['Bintray']
    pkg {
        repo = 'maven'
        name = 'StreamCompat'
        userOrg = bintrayUser
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Wrdlbrnft/StreamCompat'
        publicDownloadNumbers = true
        version {
            name = versionName
            released = new Date()
        }
    }
}